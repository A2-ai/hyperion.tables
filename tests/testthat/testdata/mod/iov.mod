$SIZES PD=100
$PROBLEM created from pharos see iov_metadata.json for details.
 
$INPUT C NUM STUDY ID ATFD ATLD NTFD NTLD TIME DOSE AMT RATE DV CMT EVID DVID BLQ LLOQ NTIME
			 BLWT  SEX RACE PLACEBO FLG OCC

$DATA test.csv IGNORE=@

$SUBROUTINE ADVAN4 TRANS4

$PK
; Covariates
		COVCLWT=THETA(7)*LOG(BLWT/70)
		ECOVCLWT = EXP(COVCLWT)

; Occasion
		IOV = ETA(7)
		IF(OCC.EQ.2) IOV = ETA(8)
		IF(OCC.EQ.3) IOV = ETA(9)
		IF(OCC.EQ.4) IOV = ETA(10)
		IF(OCC.EQ.5) IOV = ETA(11)

; Parameters
		MU_1 = THETA(1)
		KA = EXP(MU_1 + ETA(1))

		MU_2 = THETA(2) + COVCLWT
		CL = EXP(MU_2 + ETA(2))

		MU_3 = THETA(3)
		V2 = EXP(MU_3  + ETA(3))

		MU_4 = THETA(4) + COVCLWT
		Q = EXP(MU_4 + ETA(4))

		MU_5 = THETA(5)
		V3 = EXP(MU_5 + ETA(5))

		MU_6=THETA(6)
		F1=EXP(MU_6 + ETA(6) + IOV)

		S2=V2/1000

		K20 = CL/V2
		K23 = Q/V2
		K32 = Q/V3

		R = K20 + K23 + K32
		ALPHA = 0.5*(R + SQRT(R**2 - 4*K20*K32))
		BETA  = 0.5*(R - SQRT(R**2 - 4*K20*K32))
		BETATHLF = LOG(2)/BETA

$ERROR
	IPRED  = F
	SD = SQRT(EPS(1)*IPRED**2 + EPS(2))
	DUM = (LLOQ - IPRED)/SD
	CUMD = PHI(DUM)

	IF(BLQ.EQ.1) THEN
		F_FLAG = 1
		Y = CUMD
		MDVRES=1
	ELSE
		F_FLAG = 0
		Y = IPRED*(1 + EPS(1)) + EPS(2)
	ENDIF

$THETA
 (1.75)         ;KA (1/hr) :LOG
 (7.3)          ;CL (L/hr) :LOG
 (4)            ;V2 (L) :LOG
 (12)           ;Q (L/hr) :LOG
 (12.2)         ;V3 (L) :LOG
 0 FIX          ;F1 ([]) :LOG
 0.75 FIX       ;WT_on_CL ([])

$OMEGA
 0.35              ;OM1 KA :LOG
 0.15              ;OM2 CL :LOG
 0.12              ;OM3 V2 :LOG
 0 FIX             ;OM4 Q  :LOG
 0.07			   ;OM5 V3 :LOG
 0 FIX             ;OM6 F1 :LOG

$OMEGA BLOCK(1) 0.06 ;IOV :LOG
$OMEGA BLOCK(1) SAME
$OMEGA BLOCK(1) SAME
$OMEGA BLOCK(1) SAME
$OMEGA BLOCK(1) SAME

$SIGMA
 0.14         ;SIG1 :PROP
 0.05         ;SIG2 :ADD

$ESTIMATION NOABORT MAXEVAL=9999 METHOD=SAEM INTER PRINT=10 LAPLACIAN NBURN=5000 NITER=1000
$ESTIMATION NOABORT MAXEVAL=9999 METHOD=IMP INTER PRINT=1 LAPLACIAN NITER=10 EONLY=1 ISAMPLE=3000

$COV UNCONDITIONAL PRINT=E MATRIX=R

$TABLE NUM STUDY ID TIME IPRED PREDV NPDE CWRES BLQ MDV NOPRINT ONEHEADER FILE=iov.tab
$TABLE NUM KA CL V2 Q V3 F1 ETAS(1:LAST) CLI ECOVCLWT ALPHA BETA BETATHLF
			 NOAPPEND NOPRINT ONEHEADER FILE=par.tab
