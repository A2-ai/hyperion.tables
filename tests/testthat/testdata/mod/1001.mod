$SIZES PD=-100

$PROB PK Structural Model

$INPUT LINE ID STUDY ATFD=TIME NTFD ATLD NTLD DAY NDAY
$INPUT AMT RATE DUR CMT EVID DVID ODV=DV LDV MDV BLQ LLOQ
$INPUT ROUTE FREQ DOSEA DOSEN COHORT PTYPE SEXF RACE AGEBL AGECBL WTBL AEGFRBL RFCBL EXCFL

$DATA ../../../../data/derived/PK_Oral_Ex1.csv
  IGNORE=@

$SUBROUTINE ADVAN13 TOL=9

$MODEL
 COMP = (DEPOT)     ;[1] Oral Dose
 COMP = (CENT)      ;[2] Central

$PK
 ;; PK Parameters ;;
 TVCL = THETA(1)
 TVVC = THETA(2)
 TVKA = THETA(3)
 TVF1 = THETA(4)

 CL = TVCL * EXP(ETA(1))
 VC = TVVC * EXP(ETA(2))
 KA = TVKA * EXP(ETA(3))
 F1 = TVF1
 ;; PK Parameters ;;


 ;; Scaling & Rate Constants ;;
 S2   = VC/1000

 K20 = CL/VC
 ;; Scaling & Rate Constants ;;

$DES
 DADT(1) = -KA*A(1)
 DADT(2) = KA*A(1) - K20*A(2)

$ERROR (OBSERVATIONS ONLY)
  IPRED = F
  Y = IPRED*(1+EPS(1))+EPS(2)

  IRES = DV - IPRED

$THETA
 (0, 19)      ;CL/F (L/h)
 (0, 304)     ;VC/F (L)
 (0, 2)       ;KA (1/hr)
 1 FIX        ;F1 (fraction)

$OMEGA
 0.1          ;OM1 CL :EXP
 0.1          ;OM2 VC :EXP
 0.1          ;OM3 KA :EXP


$SIGMA
 0.1    ;SIG1
 2      ;SIG2


$EST   METHOD=1 INTERACTION PRINT=5 MAXEVAL=9999 NOABORT NSIG=3 SIGL=9 MSFO=1001.msf
$COV   SLOW MATRIX=R COMPRESS PRINT=E

$TABLE LINE ID STUDY TIME NTFD ATLD NTLD DAY NDAY
  CMT EVID DVID DV LDV MDV BLQ LLOQ
  ROUTE FREQ DOSEA DOSEN COHORT PTYPE SEXF RACE AGEBL AGECBL WTBL AEGFRBL RFCBL EXCFL
  PRED IPRED CWRES WRES NPDE RES
  NOPRINT NOAPPEND ONEHEADER FILE=run.output.tab FORMAT = s1PE12.5

$TABLE ID CL VC KA ETAS(1:LAST)
  FIRSTONLY NOPRINT NOAPPEND ONEHEADER FILE=run.param.tab

